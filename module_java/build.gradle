if (!cFlag.isDebug.toBoolean()) {
    apply plugin: "com.android.library"
} else {
    apply plugin: "com.android.application"
}

android {
    compileSdkVersion cAndroid.compileSdkVersion
    buildToolsVersion cAndroid.buildToolsVersion

    defaultConfig {
        if (cFlag.isDebug.toBoolean()) {
            applicationId "com.boilsnow.java"
        }

        minSdkVersion cAndroid.minSdkVersion
        targetSdkVersion cAndroid.targetSdkVersion
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    sourceSets {
        main {
            if (!cFlag.isDebug.toBoolean()) {
                manifest.srcFile "src/main/AndroidManifest.xml"
            } else {
                manifest.srcFile "src/main/debug/AndroidManifest.xml"
                java.srcDirs = ["src/main/java", "src/main/debug/java"]
                res.srcDirs = ["src/main/res", "src/main/debug/res"]
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ["*.jar"], dir: "libs")
    testImplementation "junit:junit:4.12"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    annotationProcessor "${cPage.arouterCompiler}:${cVersion.arouterCompiler}"

    implementation project(":lib_common")
}